// ==========================================
// 1. 基础配置
// ==========================================

// 隐式忽略空白字符 (空格、制表符、换行)
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

// 基础数字 (支持整数和小数，如 1, 20, 1.5)
// @ 原子规则：数字中间不允许插空格
number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

// ==========================================
// 2. 运算符定义
// ==========================================

// 算术运算符
add = { "+" }
sub = { "-" }
mul = { "*" }
idiv = { "//" }
div = { "/" }
rem = { "%" }

// 正负号
neg = { "-" }
pos = { "+" }

// 骰子操作符 (不区分大小写)
dice_op = { ^"d" }
fate_dice = { ^"f" }
coin_dice = { ^"c" }

// 比较运算符 (用于修饰符参数，如 r>5)
// 注意顺序：长匹配优先 (>= 在 > 之前)
neq = { "<>" }
gte = { ">=" }
lte = { "<=" }
gt  = { ">" }
lt  = { "<" }
eq  = { "=" }
compare_op = { neq | gte | lte | gt | lt | eq }

// ==========================================
// 3. 骰子修饰符 (后缀运算符)
// ==========================================

// 修饰符参数：可选的比较符 + 必须的数值
// 例如: >5, 3 (隐含=3)
mod_param = { compare_op? ~ atom }

// Keep / Drop
keep_high = { ^"kh" ~ atom? } // kh1, kh
keep_low  = { ^"kl" ~ atom? }
drop_high = { ^"dh" ~ atom? }
drop_low  = { ^"dl" ~ atom? }

// Limit
limit_times = { ^"lt" ~ atom } // Limit Times
limit_count = { ^"lc" ~ atom } // Limit Counts
limit = { limit_times ~ limit_count | limit_count ~ limit_times | limit_times | limit_count}

// Reroll
reroll      = { ^"r"  ~ mod_param ~ limit? }

// Explode
compound_explode = { "!!" ~ mod_param? ~ limit?}
explode          = { "!"  ~ mod_param? ~ limit?}

// Min/max
min = { ^"min" ~ atom }
max  = { ^"max" ~ atom }

// count_success
count_successes = { ^"cs" ~ mod_param}
deduct_failures = { ^"df" ~ mod_param}
subtract_failures = { ^"sf" ~ mod_param}

// 所有后缀修饰符的集合
modifier = {
    keep_high | keep_low | drop_high | drop_low |
    reroll | compound_explode | explode |
    min | max |
    count_successes | deduct_failures | subtract_failures
}

// ==========================================
// 4. 函数与列表
// ==========================================

filter = { ^"filter" ~ mod_param }

// 函数名
func_name = ${ 
    ^"floor" | ^"ceil" | ^"round" | ^"abs" | 
    ^"max" | ^"min" | ^"sum" | ^"avg" | ^"len" |
    ^"rpdice" | ^"sortd" | ^"sort" | ^"tolist" | filter }

// 参数列表: 1, 2, 3
args = { expr ~ ("," ~ expr)* }

// 函数调用: max(1, 2)
function = { func_name ~ "(" ~ args? ~ ")" }

// 列表字面量: [1, 2d6]
list = { "[" ~ args? ~ "]" }

// ==========================================
// 5. 表达式结构 (Pratt Friendly)
// ==========================================

// A. 原子
atom = {
    function |
    list |
    number |
    "(" ~ expr ~ ")" |
    "{" ~ expr ~ "}"
}

// B. 显式骰子层
// 逻辑: 一个原子，后面"可选"跟一个 d 和另一个原子(也可以是 f 或者 c)
// 只能跟一次！不能循环！
dice_expr = { atom ~ (dice_op ~ (atom | fate_dice | coin_dice))? | dice_op ~ (atom | fate_dice | coin_dice) }

// D. 运算符分类
bin_op    = _{ idiv | add | sub | mul | div | rem }
prefix_op = _{ neg | pos }
postfix_op = _{ modifier }

// E. 核心表达式
// Pratt Parser 的"原子"单位现在变成了 dice_expr
expr = { prefix_op* ~ dice_expr ~ postfix_op* ~ (bin_op ~ prefix_op* ~ dice_expr ~ postfix_op*)* }

// ==========================================
// 6. 文件入口
// ==========================================

// SOI: Start of Input, EOI: End of Input
main = _{ SOI ~ expr ~ EOI }
